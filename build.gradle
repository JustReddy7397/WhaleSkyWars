plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        maven {
            url = "https://repo.md-5.net/content/repositories/releases/"
        }

    }
}

group = 'ga.justreddy.wiki'
version = '1.0.0'

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

dependencies {
    implementation project(':api')
    implementation project(':plugin')
    implementation project(":v1_8_R3")
}


ext.set('output', "./runtime/plugins")

shadowJar {

    def output = rootProject.ext.get('output')

    destinationDirectory = file("$output")
    archiveFileName = "Testing.jar"
    relocate 'com.cryptomorin.xseries', 'ga.justreddy.wiki.whaleskywars.libs.xseries'

}

build {
    dependsOn shadowJar
}