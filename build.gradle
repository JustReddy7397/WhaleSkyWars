plugins {
    id 'java'
    id "com.gradleup.shadow" version "9.0.0"
}

allprojects {

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = "spigotmc-repo"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/groups/public/"
        }
        maven {
            url = "https://repo.md-5.net/content/repositories/releases/"
        }

    }

}


group = 'ga.justreddy.wiki'
version = '1.0.0'

def targetJavaVersion = 11
java {
    sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
    targetCompatibility = JavaVersion.toVersion(targetJavaVersion)
    toolchain {
        languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}

dependencies {
    implementation project(":bungeecord")
    implementation project(":shared")
    implementation project(':api')
    implementation project(':plugin')
    implementation project(":v1_8_R3")
    implementation project(":v1_12_R1")
    implementation project(":v1_14_R1")

}


ext.set('output', "jar/")

shadowJar {
    minimize {
        exclude(project(":shared"))
        exclude(project(":api"))
        exclude(project(":plugin"))
        exclude(project(":bungeecord"))
        exclude(project(":v1_8_R3"))
        exclude(project(":v1_12_R1"))
        exclude(project(":v1_14_R1"))
    }
    exclude("META-INF/**")
    def output = rootProject.ext.get('output')
    destinationDirectory = file("$output")
    archiveFileName = "WhaleSkyWars.jar"
    relocate "com.alessiodp.libby", "ga.justreddy.wiki.whaleskywars.libs.libby"
    relocate 'fr.mrmicky.fastboard', 'ga.justreddy.wiki.whaleskywars.libs.fastboard'
    relocate "com.fasterxml.jackson", "ga.justreddy.wiki.whaleskywars.libs.jackson"
    relocate "io.socket", "ga.justreddy.wiki.whaleskywars.libs.socketio"
    relocate "com.corundumstudio.socketio", "ga.justreddy.wiki.whaleskywars.libs.nettysocketio"
}

tasks.build {
    dependsOn(tasks.shadowJar)
}